model{

  # community priors for occupancy covariates
  for(m in 1:Vocc) {
    mu.u.b[m] ~ dnorm(0, 0.01)T(-10,10)
    tau.u.b[m] <- pow(sigma.u.b[m], -2)
    sigma.u.b[m] ~ dunif(0, 10)
  }

  # community priors for detection covariates
  for(m in 1:Vobs) {
    mu.v.b[m] ~ dnorm(0, 0.01)T(-10,10)
    tau.v.b[m] <- pow(sigma.v.b[m], -2)
    sigma.v.b[m] ~ dunif(0, 10)
  }
  
  # Wishart prior for covariance matrix
  tau.cor[1:n, 1:n] ~ dwish(ID[,], df)
  
  # loop over all species
  for (i in 1:(n)) {
    #0 mean for MVN
    mu0[i]<-0
        
    # random effects for occupancy
    for(m in 1:Vocc) {
      u.b[i, m] ~ dnorm(mu.u.b[m], tau.u.b[m])T(-10,10)
    }

    # random effects for detection
    for(m in 1:Vobs) {
      v.b[i, m] ~ dnorm(mu.v.b[m], tau.v.b[m])T(-10,10)
    }
  }  
  # loop over all sites
  for (j in 1:J) {
     e[j, 1:n] ~ dmnorm(mu0[], tau.cor[,])
     
     # loop over all species
     for (i in 1:(n)) { 
     
         # probit model for occupancy
         mu[j,i] <- inprod(u.b[i,], Xocc[j,]) + e[j,i]
         z[j,i] <- step(mu[j,i])
        
         # logistic model for detection
         logit(p[j,i]) <- inprod(v.b[i, ], Xobs[j, ])
         mu.p[j,i] <- p[j,i]*z[j,i]
         y[j,i] ~ dbin(mu.p[j,i],k[j])
     }
  }

}
